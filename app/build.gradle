buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

final int versionMajor = 0
final int versionMinor = 0
final int versionPatch = 1
final String semanticVersion = "${versionMajor}.${versionMinor}.${versionPatch}"
final String appCompatLibVersion = "23.4.0"

final int targetSdk = rootProject.targetSdkVersion;
final int minSdkRed = rootProject.minSdkVersion;

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.michaelfotiadis.crossyscore"

        compileSdkVersion rootProject.compileSdkVersion
        buildToolsVersion rootProject.buildToolsVersion

        packagingOptions.excludes = getDefaultExcludes();

        minSdkVersion minSdkRed
        targetSdkVersion targetSdk

        versionCode computeVersionCode()
        versionName semanticVersion

        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard/proguard-gson.pro',
                'proguard/reflective_drawable_loader.pro'

    }

    productFlavors {
        prod {
            applicationId "com.michaelfotiadis.crossyscore"
            resValue("string", "app_name", "Crossy Scoreboard")
        }
        dev {
            applicationId "com.michaelfotiadis.crossyscore"
            resValue("string", "app_name", "Crossy Scoreboard Dev")
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            debuggable false
            buildConfigField "Boolean", "DEV_MODE", "false"
        }

        debug {
            testCoverageEnabled true
            debuggable true
            buildConfigField "Boolean", "DEV_MODE", "true"
        }

        applicationVariants.all { final variant ->
            variant.outputs.each { final output ->
                annotateApkFilename(variant, output);
            }
        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':core')


    compile "com.android.support:appcompat-v7:$appCompatLibVersion"
    compile "com.android.support:design:$appCompatLibVersion"
    compile "com.android.support:cardview-v7:$appCompatLibVersion"
    compile "com.android.support:recyclerview-v7:$appCompatLibVersion"


    testCompile 'junit:junit:4.12'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'joda-time:joda-time:2.7'
    compile 'com.android.support:support-v4:23.3.0'
}
